// Block definition (there may be more than one block per file)
FUNCTION_BLOCK foki

// Define input variables
VAR_INPUT
    c1 : REAL;
    c2 : REAL;
    r : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    con : REAL;
END_VAR

// Fuzzify input variable 'concept1'
FUZZIFY c1
    TERM low := (0, 1) (0.2, 1) (0.5, 0); 
    TERM medium := trian 0.2 0.5 0.8;
    TERM high := (0.5, 0) (0.8, 1) (1, 1);
END_FUZZIFY

// Fuzzify input variable 'concept2'
FUZZIFY c2
    TERM low := (0, 1) (0.2, 1) (0.5, 0); 
    TERM medium := trian 0.2 0.5 0.8;
    TERM high := (0.5, 0) (0.8, 1) (1, 1);
END_FUZZIFY

// Fuzzify input variable 'relation'
FUZZIFY r
    TERM low := (0, 1) (0.2, 1) (0.5, 0); 
    TERM medium := trian 0.2 0.5 0.8;
    TERM high := (0.5, 0) (0.8, 1) (1, 1);
END_FUZZIFY


// Defzzzify output variable 'connection'
DEFUZZIFY con
    TERM inequality := (0, 1) (0.2, 1) (0.4, 0);
    TERM weak_related := trian 0.2 0.4 0.6;
    TERM closely_related := trian 0.4 0.6 0.8;
    TERM equivalent := (0.6, 0) (0.8, 1) (1, 1);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF c1 IS high AND c2 IS high AND r IS high
                THEN con IS equivalent;

    RULE 2 : IF c1 IS high AND c2 IS high AND r IS medium
                THEN con IS equivalent;

    RULE 3 : IF c1 IS high AND c2 IS high AND r IS low
                THEN con IS weak_related;

    RULE 4 : IF c1 IS high AND c2 IS medium AND r IS high
                THEN con IS equivalent;

    RULE 5 : IF c1 IS high AND c2 IS low AND r IS high
                THEN con IS closely_related;

    RULE 6 : IF c1 IS medium AND c2 IS high AND r IS high
                THEN con IS equivalent;

    RULE 7 : IF c1 IS low AND c2 IS high AND r IS high
                THEN con IS closely_related;

    RULE 8 : IF c1 IS medium AND c2 IS medium AND r IS medium
                THEN con IS weak_related;

    RULE 9 : IF c1 IS medium AND c2 IS medium AND r IS low
                THEN con IS weak_related;

    RULE 10 : IF c1 IS medium AND c2 IS medium AND r IS high
                THEN con IS closely_related;

    RULE 11 : IF c1 IS medium AND c2 IS low AND r IS medium
                THEN con IS weak_related;

    RULE 12 : IF c1 IS medium AND c2 IS high AND r IS medium
                THEN con IS closely_related;

    RULE 13 : IF c1 IS low AND c2 IS medium AND r IS medium
                THEN con IS weak_related;

    RULE 14 : IF c1 IS high AND c2 IS medium AND r IS medium
                THEN con IS closely_related;

    RULE 15 : IF c1 IS low AND c2 IS low AND r IS low
                THEN con IS inequality;

    RULE 16 : IF c1 IS low AND c2 IS low AND r IS medium
                THEN con IS inequality;

    RULE 17 : IF c1 IS low AND c2 IS low AND r IS high
                THEN con IS weak_related;

    RULE 18 : IF c1 IS low AND c2 IS medium AND r IS low
                THEN con IS inequality;

    RULE 19 : IF c1 IS low AND c2 IS high AND r IS low
                THEN con IS inequality;

    RULE 20 : IF c1 IS medium AND c2 IS low AND r IS low
                THEN con IS inequality;

    RULE 21 : IF c1 IS high AND c2 IS low AND r IS low
                THEN con IS inequality;

END_RULEBLOCK

END_FUNCTION_BLOCK